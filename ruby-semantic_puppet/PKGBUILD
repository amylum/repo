# Maintainer: Tim Meusel <tim@bastelfreak.de>
_gemname=semantic_puppet
pkgname="ruby-$_gemname"
pkgver=1.1.0
pkgrel=1
pkgdesc='Tools used by Puppet to parse, validate, and compare Semantic Versions and Version Ranges and to query and resolve module dependencies.'
arch=('any')
url='https://github.com/puppetlabs/semantic_puppet'
depends=('ruby')
makedepends=('ruby-rdoc' 'rubygems')
license=('Apache')
options=('!emptydirs')
source=("https://rubygems.org/gems/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
sha512sums=('22d5596f445faa7c7a779c11e4a9b52e1ca3f77fcc63270798e4b0fc7dddd59c66bab1644214f1526a0fa5732e349522e8664ea6285f0ca107f35009e8a1d151')
package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    --verbose \
    ${_gemname}-${pkgver}.gem

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"

  install -D -m644 "${pkgdir}${_gemdir}/gems/semantic_puppet-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
