# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libxcursor
pkgver=1.2.1
pkgrel=3
pkgdesc="X cursor management library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxfixes' 'libxrender' 'default-cursors')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/lib/libXcursor-${pkgver}.tar.xz{,.sig})
sha512sums=('ebb42a39855aab86d0c11fe6ad33d011cf56a6dc5c309bef643460319c03df239b7e09fe88777bb25c0e9ff8aceac6bf66e51cc04a2d5ee463b3bc3bbf1674e3'
            'SKIP')
#validpgpkeys=('C41C985FDCF1E5364576638B687393EE37D128F8') # "Matthieu Herrb <matthieu.herrb@laas.fr>"
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd libXcursor-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXcursor-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
