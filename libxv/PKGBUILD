# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libxv
pkgver=1.0.12
pkgrel=1
pkgdesc="X11 Video extension library"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('libxext')
makedepends=('xorgproto')
source=(${url}/releases/individual/lib/libXv-${pkgver}.tar.xz{,.sig})
sha512sums=('bdd00de1ce6a8911056181ab4de32f9e319a68567389162d4bc2e3c9946d5f3c60f87eb6c53f74c9b6a342255e591d5fef4a445a4ac8ca3c1b6fe87d42c51c1e'
            'SKIP')
#validpgpkeys=('C41C985FDCF1E5364576638B687393EE37D128F8') # Matthieu Herrb <matthieu.herrb@laas.fr>
validpgpkeys+=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd libXv-${pkgver}
  ./configure --prefix=/usr --disable-static 
  make
}

package() {
  cd libXv-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
