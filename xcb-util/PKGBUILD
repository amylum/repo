# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=xcb-util
pkgver=0.4.1
pkgrel=1
pkgdesc="Utility libraries for XC Binding"
arch=('x86_64')
url="https://xcb.freedesktop.org"
license=('custom')
depends=('libxcb>=1.7')
makedepends=('gperf' 'xorg-util-macros' 'xorgproto')
source=(https://xcb.freedesktop.org/dist/${pkgname}-${pkgver}.tar.xz{,.sig})
sha512sums=('da67f2f017d2a1788dcf35f28d6956e171303a622a1dd085cd3d69fdb2ed77965d83c557cc926ebf9b32e905eb2cbb5921987250192d78a2f5edc4d437ed7d2b'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alanc@freedesktop.org>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() { 
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -D -m644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
